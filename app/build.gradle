apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.tengu.sharetoclipboard"
        minSdkVersion 15
        targetSdkVersion 31
        versionCode 31
        versionName "1.15.1"
    }


    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    lint {
        disable 'MissingTranslation'
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingKeyAlias=alias
     *
     * and set the KSTOREPWD env var to the store and key passwords (should be the same)
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingKeyAlias')) {
         println "Found sign properties in gradle.properties! Signing buildâ€¦"

         signingConfigs {
              release {
                   storeFile file(signingStoreLocation)
                   storePassword System.getenv("KSTOREPWD")
                   keyAlias signingKeyAlias
                   keyPassword System.getenv("KSTOREPWD")

              }
         }
         buildTypes.release.signingConfig = signingConfigs.release
    } else {
         buildTypes.release.signingConfig = null
    }
}

dependencies {
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.10.6'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
}
